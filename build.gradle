plugins {
    id 'org.springframework.boot' version '3.1.0'
    id 'io.spring.dependency-management' version '1.1.0'
    id 'java'
    id 'jacoco'
    id 'org.asciidoctor.jvm.convert' version '3.3.2'
}

group = 'com.example'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '17'

ext {
    snippetsDir = file('build/generated-snippets')
}

configurations {
    asciidoctorExt
}

// https://stackoverflow.com/questions/75732794/spring-boot-3-and-swagger-ui-java-lang-nosuchmethoderror-io-swagger-v3-oas-ann
configurations.all {
    resolutionStrategy {
        eachDependency { details ->
            if (details.requested.group == 'io.swagger.core.v3') {
                details.useVersion("2.2.8")
                details.because('Swagger UI and Kafka Avro incompatible dependency')
            }
        }
    }
}

asciidoctor {
    dependsOn test
    configurations 'asciidoctorExt'
    sourceDir 'src/docs/asciidoc'
    outputDir  file('build/docs')
    attributes \
        'snippets': snippetsDir
    dependsOn test
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required = false
        csv.required = false
        html.outputLocation = layout.buildDirectory.dir('jacoco/test/html/')
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.integration:spring-integration-jmx'
    implementation 'io.springfox:springfox-boot-starter:3.0.0'
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.1.0'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
    testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'

    asciidoctorExt 'org.springframework.restdocs:spring-restdocs-asciidoctor'
}

test {
    outputs.dir snippetsDir
    useJUnitPlatform()
    finalizedBy jacocoTestReport
    finalizedBy asciidoctor
}
