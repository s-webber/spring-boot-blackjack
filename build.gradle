buildscript {
   repositories {
      jcenter()
   }
   dependencies {
      classpath("org.springframework.boot:spring-boot-gradle-plugin:1.4.3.RELEASE")
   }
}

plugins { 
   id "org.asciidoctor.convert" version "1.5.3"
}

apply plugin: 'java'
apply plugin: 'org.springframework.boot'
apply plugin: 'jacoco'
apply plugin: 'pmd'
apply plugin: 'findbugs'
apply plugin: 'checkstyle'

check.dependsOn jacocoTestReport

jar {
   baseName = 'spring-boot-blackjack'
   version =  '0.1.0'
}

repositories {
   jcenter()
}

springBoot {
   requiresUnpack = ['org.ajar:swagger-spring-mvc-ui']
}

checkstyle {
   toolVersion = '7.4'
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

ext {
   snippetsDir = file('build/generated-snippets')
}

dependencies {
   compile "org.springframework.boot:spring-boot-starter-web"
   compile "org.springframework.boot:spring-boot-starter-security"
   compile "org.springframework.integration:spring-integration-jmx"
   compile "io.springfox:springfox-swagger2:2.6.1"
   compile "io.springfox:springfox-swagger-ui:2.6.1"

   testCompile "org.springframework.boot:spring-boot-starter-test"
   testCompile "org.springframework:spring-test"
   testCompile "org.springframework.restdocs:spring-restdocs-mockmvc"
}

test { 
   outputs.dir snippetsDir
}

asciidoctor { 
   attributes 'snippets': snippetsDir 
   inputs.dir snippetsDir 
   dependsOn test
}

jar {
    dependsOn asciidoctor
    from ("${asciidoctor.outputDir}/html5") {
        into 'static/docs'
    }
}

task wrapper(type: Wrapper) {
   gradleVersion = '3.0'
}
